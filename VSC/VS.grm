"Name"    = 'V#' 
"Version" = '2016'
"Author"  = 'Arsslen Idadi' 
"About"   = 'V.'

"Case Sensitive" = True
"Start Symbol"   = <COMPILATION UNIT>
                

                                                              
{Id Head}        = {Letter} + [_]
{Id Tail}        = {Id Head} + {Digit}
{Hex Digit}      = {Digit} + [abcdef] + [ABCDEF]
{Oct Digit}      = [01234567]
{Binary Digit}   = [01]                 
{Separated Digit} = {Digit} + [_]  
{Separated Hex Digit} = {Hex Digit} + [_]  
{Separated Binary Digit} = {Binary Digit} + [_]  
{Separated Oct Digit}   =  {Oct Digit}    + [_]   
                        
{String Ch}      = {Printable} - ["]
{Char Ch}        = {Printable} - ['']         
{Asm Char}     =  {Printable} - [}] + {CR} + {LF}                      
! ----------------------------------------------------------------- Terminals

RealLiteral       =  {Digit}*'.'{Digit} + ([eE] + [+-]?)? {Digit}*[FfDd]?
                    | {Digit} + ([eE] + [+-]? {Digit}+ [FfDd]? |  [FfDd])
           

DecLiteral        = ({Digit} | {Digit}{Separated Digit}*{Digit}) + ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
BinaryLiteral     = '0'[bB]({Binary Digit} | {Binary Digit}{Separated Binary Digit}*{Binary Digit}) + ( [UuLl] | [Uu][Ll] | [Ll][Uu] )? 
OctalLiteral      = '0'[oO]({Oct Digit} | {Oct Digit}{Separated Oct Digit}*{Oct Digit}) + ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?
HexLiteral        = '0'[xX]({Hex Digit} | {Hex Digit}{Separated Hex Digit}*{Hex Digit}) + ( [UuLl] | [Uu][Ll] | [Ll][Uu] )?                                      
                 
Identifier        = [@]?{Id Head}{Id Tail}* 
RegularStringLiteral  =  '"'( {String Ch} | '\'{Printable} )* '"'  
VerbatimStringLiteral =  '@"'{String Ch}* '"'  
CharLiteral      = '' ( {Char Ch} | '\'{Printable}* )''                                                                         
OperatorLiteralUnary  =  '@' [!&%*+-/<>=?\^|~£¤µ]+
OperatorLiteralBinary  =  '@@' [!&%*+-/<>=?\^|~£¤µ]+ 
ASMCodeLiteral = 'asm' {Whitespace}* '{' {Asm Char} '}'
                       
                                                                 
! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'  
Documentation Line  = '///'
                    
<Documentation> ::= Documentation <Documentation> 
                 | Documentation
                 
<Opt Documentation> ::= <Documentation>
                     | 
! ===================================================================
! Terminals
! ===================================================================
<Identifier> ::= Identifier
<Float Constant> ::= RealLiteral
<Null Constant> ::= null                              
<Boolean Constant> ::= true
                   | false
<Character Constant> ::= CharLiteral

<Unary Operator Constant>  ::= OperatorLiteralUnary
<Binary Operator Constant>  ::=  OperatorLiteralBinary  
                            
                                                                                         
! ===================================================================
! Types
! ===================================================================
<package or type expr> ::= <Qualified Alias Member>
                        |  <simple name expr>
                        | <package or type expr> '.' <simple name expr>      
                        
<Qualified Alias Member> ::=  <Identifier>  '::'   <simple name expr>                                    
<simple name expr> ::=  <Identifier>  <opt type argument list>               

<opt type argument list>
        ::= 
         |  '!<' <type arguments> '>' ! conflict error
         
<type arguments>
        ::= <type> 
         |  <type arguments> ',' <type> 

<opt type parameter list>
        ::= 
         |  '<' <type parameters> '>' 

<type parameters>
        ::= <Identifier> 
         |  <type parameters> ',' <Identifier> 

<type> ::= <type expression or array>
<member type> ::= <type expression or array>
               | void       

<type expression or array> ::= <type expression>
        | <type expression> <rank specifiers>


<rank specifiers>
        ::= <rank specifier> 
         |  <rank specifier> <rank specifiers> 

<rank specifier>
        ::= '[]' 
         |  '[,' <dim separators> ']' ! conflict error (type with element access)

<dim separators>
        ::= ',' 
         |  <dim separators> ',' 
         
<type expression>  ::= <package or type expr>      
                   | <package or type expr>     '?' ! conflict warning
                   | <package or type expr>     <pointer stars>   
                    | <builtin type expression>
                    
<builtin type expression> ::= <builtin type>
                           | <builtin type> '?' ! conflict warning
                           | <builtin type> <pointer stars>  
  
<builtin type> ::=   float
         |  double
         |  bool 
         |  object
         |  string
         | <integral type>

<integral type> ::= sbyte 
         |  byte 
         |  short
         |  ushort
         |  int
         |  uint 
         |  long 
         |  ulong
         |  char

<pointer stars> ::= '*' ! conflict warning
                 | <pointer stars> '*'  ! conflict warning
                                                                         

  
<opt type parameter constraints clauses>
        ::= 
         |  <type parameter constraints clauses> 

<type parameter constraints clauses>
        ::= <type parameter constraints clause> 
         |  <type parameter constraints clauses> <type parameter constraints clause> 

<type parameter constraints clause>
        ::= where <Identifier>  ':' <type parameter constraints> 


<type parameter constraints>
        ::= <type parameter constraint> 
         |  <type parameter constraints> ',' <type parameter constraint> 

<type parameter constraint>
        ::= <type> 
         |  class 
         |  struct
         |  self '(' ')'
         
<opt class base>
        ::= 
         |  <class base> 

<type list>
        ::= <type>
         |  <type list> ',' <type>
         
<class base>
        ::= ':' <type list> 
         
! ===================================================================
! Arguments, Parameters, Initializers & Modifiers
! =================================================================== 

<opt formal parameter list>
        ::= 
         |  <formal parameter list> 


<formal parameter list>
        ::= <fixed parameters> 
         |  <fixed parameters> ',' <parameter array> 
  

<fixed parameters>
        ::= <fixed parameter> 
         |  <fixed parameters> ',' <fixed parameter> 

<fixed parameter>
        ::= <opt attributes> <opt parameter modifier> <type> <Identifier> 
         |  <opt attributes> <opt parameter modifier> <type> <Identifier>  '=' <expression> 
    
<opt parameter modifier>
        ::= 
         |  <parameter modifier>
         
<parameter modifier>
        ::= ref 
         |  out 
         |  self 
         
<parameter array>
        ::= <opt attributes> params <type> <Identifier>  
         |  <opt attributes> params <type> <Identifier>  '='  <expression>  
         
<opt argument list>
        ::= 
         |  <argument list> 

<argument list>
        ::= <argument> 
         |  <argument list> ',' <argument> 
          
         

<argument>
        ::= <expression> 
         |  <named argument expression>
         |  <non simple argument> 

<non simple argument>
        ::= ref <expression> 
         |  out <expression> 
 
<named argument expression> ::= <identifier> ':' <expression>
                             

<opt array initializer>
        ::= 
         |  <array initializer> 

<array initializer>
        ::= '{' '}'
         |  '{' <variable initializer list> <opt comma> '}' 

<variable initializer list>
        ::= <variable initializer> 
         |  <variable initializer list> ',' <variable initializer> 
      
<variable initializer>
        ::= <expression> 
         |  <array initializer>
         
 <opt rank specifier>
        ::= 
         |  <rank specifiers>            
 <opt comma> ::= ','
              | 
              
<opt modifiers>
        ::= 
         |  <modifiers> 

<modifiers>
        ::= <modifier> 
         |  <modifiers> <modifier> 

<modifier>
       ::= abstract
        |  extern
        |  new
        |  override
        |  sync 
        |  readonly
        |  sealed
        |  static
        |  supersede
        |  virtual
        |  private
        |  protected
        |  public
        |  internal      !Friend
         
<opt partial> ::= 
               | partial
! ===================================================================
! Attributes
! =================================================================== 
<opt attributes> ::= 
                          | <attribute sections>    
                          
<attribute sections>
        ::= <attribute section> 
         |  <attribute sections> <attribute section> 
         
<attribute section>
       ::= '[' <opt attribute target>  <attribute list> ']'
        |  '[' <opt attribute target>  <attribute list> ',' ']'
        
 <opt attribute target> 
       ::= return   ':'
        |  ! Nothing
        
<attribute list>
       ::= <attribute>
        |  <attribute list>',' <attribute>

<attribute>
       ::= <package or type expr> '(' <opt argument List> ')'
        |  <package or type expr>                         
! ===================================================================
! Expressions
! =================================================================== 
<expression list>
        ::= <expression>
         |  <expression list> ',' <expression>

<expression>
        ::= <assignment expression> 
         |  <non assignment expression> 
         
<assignment expression>
        ::= <prefixed unary expression> '=' <expression> 
         |  <prefixed unary expression> '*=' <expression> 
         |  <prefixed unary expression> '/=' <expression> 
         |  <prefixed unary expression> '%=' <expression> 
         |  <prefixed unary expression> '+=' <expression> 
         |  <prefixed unary expression> '-=' <expression> 
         |  <prefixed unary expression> '<<=' <expression> 
         |  <prefixed unary expression> '>>=' <expression> 
         |  <prefixed unary expression> '<~=' <expression> 
         |  <prefixed unary expression> '~>=' <expression> 
         |  <prefixed unary expression> '&=' <expression> 
         |  <prefixed unary expression> '|=' <expression> 
         |  <prefixed unary expression> '^=' <expression> 
         

<non assignment expression>
        ::= <conditional expression> 
         |  <lambda expression> 
         
<lambda expression>
        ::= <Identifier>  '=>' <lambda expression body> 
         |  '(' <opt lambda parameter list> ')' '=>' <lambda expression body> 
        
<lambda parameter list>
        ::= <lambda parameter> 
         |  <lambda parameter list> ',' <lambda parameter> 

<lambda parameter>
        ::= <member type> <Identifier>  

 

<opt lambda parameter list>
        ::= 
         |  <lambda parameter list> 

<lambda expression body>
        ::= <expression> 
         |  <block statement> 

<conditional expression>
        ::= <null coalescing expression> 
         |  <null coalescing expression> '?' <expression> ':' <expression>   
       
<null coalescing expression>
        ::= <null coalescing expression> '??' <Binary Operation Expression>
         |  <Binary Operation Expression>
     
<Binary Operation Expression>
        ::= <Binary Operation Expression> <Binary Operator Constant> <conditional or expression>
         |  <conditional or expression>
             
<conditional or expression>
        ::= <conditional or expression> '||' <conditional and expression> 
         | <conditional and expression> 
         
<conditional and expression>
        ::= <conditional and expression> '&&' <inclusive or expression> 
         |  <inclusive or expression> 
         
<inclusive or expression>
        ::= <inclusive or expression> '|' <exclusive or expression>
         |  <exclusive or expression>
         
<exclusive or expression>
        ::= <exclusive or expression> '^' <and expression> 
         |  <and expression> 
         
<and expression>
        ::= <and expression> '&' <equality expression> 
         |  <equality expression> 
         
<equality expression>
        ::= <relational expression> 
         |  <equality expression> '==' <relational expression> 
         |  <equality expression> '!=' <relational expression>
         
<relational expression>
        ::= <shift expression> 
         |  <relational expression> '<' <shift expression> 
         |  <relational expression> '>' <shift expression> 
         |  <relational expression> '<=' <shift expression> 
         |  <relational expression> '>=' <shift expression> 
         
<shift expression>
        ::= <shift expression> '<<' <additive expression> 
         |  <shift expression> '>>' <additive expression> 
         |   <shift expression> '<~' <additive expression> 
         |  <shift expression> '~>' <additive expression> 
         |  <additive expression> 
         

<additive expression>
        ::= <additive expression> '+' <multiplicative expression> 
         |  <additive expression> '-' <multiplicative expression> 
         |  <additive expression> as <type>
         |  <additive expression> is <type>
         |  <multiplicative expression> 

         
<multiplicative expression>
        ::= <multiplicative expression> '*' <prefixed unary expression> 
         |  <multiplicative expression> '/' <prefixed unary expression> 
         |  <multiplicative expression> '%' <prefixed unary expression> 
         |  <prefixed unary expression> 
         
<prefixed unary expression>
        ::= <address of expression>
         |  <positive expression> 
         |  <negation expression>  
         |  <indirection expression>
         |  <pre increment expression>
         |  <pre decrement expression>
         |  <unary operation expression>
         |  <unary expression> 
  
<address of expression> ::= '&' <unary expression>
<positive expression> ::= '+' <unary expression> 
<negation expression> ::= '-' <unary expression> 
<indirection expression> ::= '*' <unary expression> 
<pre increment expression> ::= '++' <unary expression> 
<pre decrement expression> ::=  '--' <unary expression>     
<unary operation expression> ::=   <Unary Operator Constant> <unary expression>  
<logical negation expression> ::= '!' <prefixed unary expression> 
<ones complement expression>  ::= '~' <prefixed unary expression> 
<cast expression> ::= '$(' <type> ')' <prefixed unary expression> ! conflict                             
                                              
<unary expression>
        ::= <primary expression> 
         | <logical negation expression> 
         | <ones complement expression>
         |  <cast expression>

  
<Constant Expression>  ::= <Multiline String Constant>
                        | <Character Constant>  
                        | <Integral Constant> 
                        | <Boolean Constant>
                        | <Null Constant>   
                        | <Float Constant>
                        
<Integral Constant> ::= OctalLiteral   
                   | HexLiteral
                   | DecLiteral  
                   | BinaryLiteral
                                        
<Multiline String Constant> ::= RegularStringLiteral
                    | RegularStringLiteral <Multiline String Constant>
                    | VerbatimStringLiteral
                    | VerbatimStringLiteral <Multiline String Constant>      
<default value expression>
        ::= default '(' <type> ')'  
<typeof expression>
        ::= typeof '(' <member type> ')' 
<sizeof expression>
        ::= sizeof '(' <type> ')' 
<checked expression>
        ::= checked '(' <expression> ')'    
<unchecked expression>
        ::= unchecked '(' <expression> ')' 
<pointer member access>
        ::= <primary expression> '->' <simple name expr> 
<super access>
        ::= super '[' <expression list> ']' 
                  
<post increment expression>
        ::= <primary expression> '++' 
<post decrement expression>
        ::= <primary expression> '--' 
<element access>
        ::= <primary expression> '[' <expression list> ']'        
         | <primary expression> '?[' <expression list> ']' 
<parenthesized expression>
        ::= '(' <expression> ')'

<member access>
        ::= <accessible primary expression> '.' <Identifier>  <opt type argument list> 
        | <accessible primary expression> '?.' <Identifier>  <opt type argument list> 
        | <package or type expr>
        |  super '.' <Identifier>  <opt type argument list>       
        |  super '?.' <Identifier>  <opt type argument list>        
          
<invocation expression>
        ::= <primary expression> '(' <opt argument list> ')'
         
<anonymous type expression>
        ::= new '{' <anonymous type parameters opt comma> '}' 
<anonymous type parameters opt comma>
        ::= <anonymous type parameters opt> 
         |  <anonymous type parameters> ',' 
<anonymous type parameters opt>
        ::= 
         |  <anonymous type parameters> 
<anonymous type parameters>
        ::= <anonymous type parameter> 
         |  <anonymous type parameters> ',' <anonymous type parameter> 
<anonymous type parameter>
        ::= <Identifier>  '=' <variable initializer> 
 
<object or delegate creation expression>
        ::= new <type expression> '(' <opt argument list> ')'

<array creation expression>
        ::= new <type expression> '[' <expression list> ']' <opt rank specifier> <opt array initializer> 
         |  new <type expression> <rank specifiers>
         |  new <rank specifier> <array initializer>         


        
<primary expression>
        ::=  <member access> 
         |   <accessible primary expression>
         | <Constant Expression> 
         
<accessible primary expression>
         ::=   <parenthesized expression> 
         |  <default value expression> 
         |  <invocation expression> 
         |  <element access> 
         |  self 
         |  <super access> 
         |  <post increment expression> 
         |  <post decrement expression> 
         |  <typeof expression> 
         |  <sizeof expression> 
         |  <checked expression> 
         |  <unchecked expression> 
         |  <pointer member access> 
         |  <object or delegate creation expression> 
         |  <anonymous type expression> 
         |  <array creation expression> 
 
<Expression Statement> ::=  <invocation expression>   ';'
                         |  <post increment expression> ';'
                         |  <post decrement expression>   ';'
                         |  <pre increment expression> ';'
                         |  <pre decrement expression> ';' 
                         |  <assignment expression> ';'
! ===================================================================
! Registers Operations
! ===================================================================  

<Register> ::=   '$' <Identifier>            
<Register Expression> ::= <Register> ':=' <Register Target Expression>  ! Register Assign
                        | <Expression> ':=' <Register> ! Var Assign
                        | '+' <Register> ! Push
                        | '-' <Register>  ! Pop
                        | <Register> ':='  <Register Operation> '?' <Register Target Expression>  ':' <Register Target Expression>  ! If Expr
                        | <Register Operation> 
                        

<Register Operation> ::= <Register> <Register Binary Operator> <Register>
                         | <Register Unary Operator> <Register>
                         
<Register Target Expression> ::= <Expression>                      
                              | <Register>
                              

<Register Binary Operator> ::= '=='
                   | '!='
                   | '<='
                   | '>='
                   | '>'
                   | '<'
                   | '+'
                   | '-'
                   | '*'
                   | '/'
                   | '%'
                   | '^'
                   | '&'
                   | '|'
                   | '<<'
                   | '>>'
                   | '<~'
                   | '~>'
                                      
<Register Unary Operator> ::= '++'
                  | '--'
                  | '~'
                  | '!'
                  | '?!'
                  | '??'
                  | '&'
                  | '*'
! ===================================================================
! Statements
! ===================================================================          
<block statement> 
        ::= '{' <opt statement list> '}'

<Expression Opt> ::= <expression>
                  |
<opt statement list>
        ::= 
         |  <statement list> 

<statement list>
        ::= <statement> 
         |  <statement list> <statement> 
         
<statement>
        ::= <block variable declaration> 
        |  <labeled statement>  
        |  <if statement>   
        |  <for statement>
        |  <foreach statement>
        |  <while statement> 
        |  <sync statement>
        |  <using statement>
        |  <Normal Statement>      


<Then Statement>   
       ::= <if then statement>  
        |  <for then statement>
        |  <foreach then statement>
        |  <while then statement>
        |  <sync then statement>
        |  <using then statement>
        |  <Normal Statement>     
          
          
          
<Normal Statement>                   
       ::= <switch statement>
        |  <do while statement> 
        |  <try statement> 
        |  <checked statement>   
        |  <break statement>
        |  <continue statement>
        |  <if continue statement>
        |  <goto label statement>
        |  <goto case statement> 
        |  <goto default statement>
        |  <return statement> 
        |  <Throw Statement>
        |  <Expression Statement>     
        |  <Register Expression statement>     
        |  <interrupt statement> 
        |  <delete statement>
        |  <Asm Code Statement>
        |  <empty statement>
        |  <block statement>    

<register expression statement> ::= <Register Expression>  ';' 
<Throw Statement> ::= throw <Expression Opt> ';'
<return statement> ::= return <Expression Opt> ';'
<goto case statement> ::= goto case <Expression> ';'
<goto label statement> ::= goto <Identifier>  ';'
<goto default statement> ::= goto default ';'
<break statement> ::= break <expression> ';'
                   |  break ';'
<continue statement> ::=  continue ';'
<if continue statement> ::= if continue ';'
<checked statement> ::= checked <block statement>  
<try statement> ::= try <block statement>  <Catch Clauses> <Finally Clause Opt>
<do while statement> ::= do <Normal Statement>    while '(' <Expression> ')' ';'
<switch statement> ::=  switch '(' <Expression> ')' '{' <Switch Sections Opt> '}'
<Asm Code Statement> ::= ASMCodeLiteral
                      

<if statement> ::=  if       '(' <Expression> ')' <Statement>
                | if       '(' <Expression> ')' <Then Statement>   else <Statement>      
<for statement> ::= for      '(' <For Init Opt> ';' <For Condition Opt> ';' <For Iterator Opt> ')' <Statement>
<foreach statement> ::= foreach  '(' <Type> <Identifier>  in <Expression> ')' <Statement>  
<while statement>  ::=  while    '(' <Expression> ')' <Statement>                                
<sync statement> ::= sync     '(' <Expression> ')' <Statement>
<using statement> ::= using    '(' <Resource>   ')' <Statement>     

<if then statement> ::=if       '(' <Expression> ')' <Then Statement>   else <Then Statement>     
<for then statement> ::= for      '(' <For Init Opt> ';' <For Condition Opt> ';' <For Iterator Opt> ')' <Then Statement>  
<foreach then statement> ::= foreach  '(' <Type> <Identifier>  in <Expression> ')' <Then Statement>  
<while then statement> ::=while    '(' <Expression> ')' <Then Statement>  
<using then statement> ::= using    '(' <Resource>   ')' <Then Statement>   
<sync then statement> ::=   sync     '(' <Expression> ')' <Then Statement>                                                                  


<delete statement> ::=  delete <expression> ';'
<interrupt statement> ::=   interrupt  <Integral Constant>  ';'
        |  interrupt <Integral Constant> '(' <expression list> ')' ';'
<empty statement> ::= ';'

<labeled statement>
        ::= <Identifier>  ':' <statement> 
         
<block variable declaration>
        ::= <type> <Identifier>  <opt local variable initializer> <opt variable declarators> ';'  
         |  const <type> <Identifier>  <const variable initializer> <opt const declarators> ';'  

<opt local variable initializer>
        ::= 
         |  '=' <block variable initializer> 
   

<opt variable declarators>
        ::= 
         |  <variable declarators> 


<variable declarators>
        ::= <variable declarator> 
         |  <variable declarators> <variable declarator> 

<variable declarator>
        ::= ',' <Identifier>  
         |  ',' <Identifier>  '=' <block variable initializer> 


<const variable initializer>
        ::= 
         |  '=' <variable initializer> 

<opt const declarators>
        ::= 
         |  <const declarators> 

<const declarators>
        ::= <const declarator> 
         |  <const declarators> <const declarator> 

<const declarator>
        ::= ',' <Identifier>  '=' <variable initializer> 
<block variable initializer>
        ::= <variable initializer> 
         |  stackalloc <type expression> '[' <expression> ']' 
         |  stackalloc <type expression> 

! ===========================================================================
! Switch Clauses
! ===========================================================================

<Switch Sections Opt>
        ::= <Switch Sections Opt> <Switch Section>
         |  !Nothing

<Switch Section>
        ::= <Switch Labels> <statement list>

<Switch Labels>
        ::= <Switch Label>
         |  <Switch Labels> <Switch Label>

<Switch Label>
        ::= case <Expression> ':'
         |  default ':'
         

! ===========================================================================
! For Clauses
! ===========================================================================

<For Init Opt>
        ::= <block variable declaration>
         |  <Statement Exp List>
         |  !Nothing

<For Iterator Opt>
        ::= <Statement Exp List>
         |  !Nothing 

<For Condition Opt>
        ::= <Expression>
         |  !Nothing 

<Statement Exp List>
        ::= <Statement Exp List> ',' <Expression>
         |  <Expression>

! ===========================================================================
! Catch Clauses
! ===========================================================================

<Catch Clauses>
        ::= <Catch Clause> <Catch Clauses>
         |  !Nothing

<Catch Clause>
        ::= catch '(' <package or type expr> <Identifier>  ')' <block statement> 
         |  catch <block statement> 

<Finally Clause Opt>
        ::= finally <block statement> 
         |  !Nothing

! ===========================================================================
! Using Clauses
! ===========================================================================

<Resource>
        ::= <block variable declaration>
         |  <expression>
         
! ===================================================================
! Declarations
! ===================================================================   

<compilation unit>
       ::=  <import directives> <package declarations>     

<package declarations> ::= 
                        | <package declarations> <package declaration>                 
<import directives>
       ::= <import directives> <import directive>
        |  !Nothing 
     
<import directive>
       ::= import <Identifier>  '=' <package or type expr> ';'
        |  import <package or type expr> ';'
    
<opt semicolon> ::= ';'
                 | 
                                  
<package declaration> ::= <opt attributes> package <package or type expr> '{' <import directives> <package or type declarations> '}' <opt semicolon>  
    
                                    
<package or type declarations>  
       ::= <package or type declarations> <package or type declaration>  
        |  ! Zero or more

<package or type declaration>  
       ::= <package declaration>
        |  <type declaration>   !Default package
        |  <global member declaration> 


<type declaration>
        ::= <class declaration> 
         |  <struct declaration> 
         |  <union declaration>
         |  <interface declaration> 
         |  <enum declaration> 
         |  <delegate declaration> 
         
 
<global member declaration>      ::= <constant declaration> 
                                |  <field declaration> 
                                |  <method declaration> 
                                |  <property declaration> 
                                |  <event declaration> 
                                |  <indexer declaration> 
                                |  <operator declaration>    
                                |  <interrupt declaration>
                                    
<opt class member declarations>
        ::= 
         |  <class member declarations> 

<class member declarations>
        ::= <class member declaration> 
         |  <class member declarations> <class member declaration> 

<class member declaration>
        ::= <constant declaration> 
         |  <field declaration> 
         |  <method declaration> 
         |  <property declaration> 
         |  <event declaration> 
         |  <indexer declaration> 
         |  <operator declaration> 
         |  <constructor declaration> 
         |  <destructor declaration> 
         |  <type declaration> 
         

<struct declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <opt partial> struct <type declaration name><opt class base> <opt type parameter constraints clauses> '{' <opt class member declarations> '}' <opt semicolon> 
<union declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <opt partial> union <type declaration name><opt class base> <opt type parameter constraints clauses> '{' <opt class member declarations> '}' <opt semicolon> 
        

<constant declaration>
        ::= <Opt Documentation> <opt attributes> <opt modifiers> const <type> <Identifier> '=' <variable initializer> <opt constant declarators> ';' 

<opt constant declarators>
        ::= 
         |  <constant declarators> 

<constant declarators>
        ::= <constant declarator> 
         |  <constant declarators> <constant declarator> 

<constant declarator>
        ::= ',' <Identifier> '=' <variable initializer>   

<field declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <member type> <Identifier>  <opt field initializer> <opt field declarators> ';'  
    
<opt field initializer>
        ::= 
         |  '=' <variable initializer> 

<opt field declarators>
        ::= 
         |  <field declarators> 

<field declarators>
        ::= <field declarator> 
         |  <field declarators> <field declarator> 

<field declarator>
        ::= ',' <Identifier>  
         |  ',' <Identifier>  '=' <variable initializer> 
         
<method declaration>
        ::= <Opt Documentation>  <method header> <method body expression block> 
         
<method header>
        ::= <opt attributes> <opt modifiers> <member type> <method declaration name> '(' <opt formal parameter list> ')' <opt type parameter constraints clauses> 
         |  <opt attributes> <opt modifiers> <member type> <modifiers> <method declaration name> '(' <opt formal parameter list> ')' <opt type parameter constraints clauses> 
      
<method body expression block>
        ::= <block or semicolon>
         |  <expression block> 

<block or semicolon>
        ::= <block statement>  
         |  ';' 

<expression block>
        ::= '=>' <expression> ';' 
         
<property declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <member type> <method declaration name>  '{' <accessor declarations> '}' <opt property initializer> 
         | <Opt Documentation>  <opt attributes> <opt modifiers> <member type> <method declaration name>  <expression block> 

<opt property initializer>
        ::= 
         |  '=' <property initializer> ';' 

<property initializer>
        ::= <expression> 
         |  <array initializer> 

<indexer declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <member type> <indexer declaration name> '[' <opt formal parameter list> ']' <indexer body> 

<indexer body>
        ::= '{' <accessor declarations> '}' 
         |  <expression block> 

<accessor declarations>
        ::= <get accessor declaration> 
         |  <get accessor declaration> <accessor declarations> 
         |  <set accessor declaration> 
         |  <set accessor declaration> <accessor declarations> 
         
<get accessor declaration>
        ::= <opt attributes> <opt modifiers> get <block or semicolon> 

<set accessor declaration>
        ::= <opt attributes> <opt modifiers> set <block or semicolon> 
         
<interface declaration>
        ::=<Opt Documentation>  <opt attributes> <opt modifiers> <opt partial> interface <type declaration name> <opt class base> <opt type parameter constraints clauses> '{' <opt interface member declarations> '}' <opt semicolon> 

<opt interface member declarations>
        ::= 
         |  <interface member declarations> 

<interface member declarations>
        ::= <interface member declaration> 
         |  <interface member declarations> <interface member declaration> 

<interface member declaration>
        ::= <constant declaration> 
         |  <field declaration> 
         |  <method declaration> 
         |  <property declaration> 
         |  <event declaration> 
         |  <indexer declaration> 
         |  <operator declaration> 
         |  <constructor declaration> 



<interrupt declaration> ::= interrupt  <Integral Constant>  <block statement>
                         
<operator declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> <operator declarator> <method body expression block> 

<operator declarator>
        ::= <type> operator <overloadable operator> '(' <opt formal parameter list> ')' 
         |  implicit operator <type> '(' <opt formal parameter list> ')' 
         |  explicit operator <type> '(' <opt formal parameter list> ')'
         
<overloadable operator>
        ::= '!' 
         | '~'
         | '++'
         | '--'
         | true
         | false
         | '+'
         | '-'
         | '*'
         | '/'
         | '%'
         | '&'
         | '^'
         | '|'
         | '<<'
         | '>>'
         | '<~'
         | '~>'
         | '=='
         | '!='
         | '<'
         | '>'
         | '>='
         | '<='
         |  is
         | <Unary Operator Constant>   
         | <Binary Operator Constant>
         

 <constructor declaration>
        ::= <Opt Documentation> <constructor declarator> <block or semicolon>

<constructor declarator>
        ::= <opt attributes> <opt modifiers> self '(' <opt formal parameter list> ')' <opt constructor initializer> 

<constructor initializer>
        ::= ':' super '(' <opt argument list> ')' 
         |  ':' self '(' <opt argument list> ')'
         
 
<opt constructor initializer>
        ::= 
         |  <constructor initializer>   
         
<destructor declaration>
        ::= <Opt Documentation>  <opt attributes> <opt modifiers> '~' self '(' ')' <block or semicolon> 

<event declaration>
        ::=<Opt Documentation>  <opt attributes> <opt modifiers> event <type> <method declaration name>  <opt event initializer> <opt event declarators> ';' 
         | <Opt Documentation>  <opt attributes> <opt modifiers> event <type> <method declaration name>  '{' <event accessor declarations> '}' 


<opt event initializer>
        ::= 
         |  '=' <event variable initializer> 

<opt event declarators>
        ::= 
         |  <event declarators>   
         
<event declarators>
        ::= <event declarator> 
         |  <event declarators> <event declarator> 

<event declarator>
        ::= ',' <Identifier>  
         |  ',' <Identifier>  '=' <event variable initializer> 

<event variable initializer>
        ::= <variable initializer> 

<event accessor declarations>
        ::= <add accessor declaration> <remove accessor declaration> <raise accessor declaration opt>
         |  <remove accessor declaration> <add accessor declaration> <raise accessor declaration opt>
         |  <add accessor declaration>  <raise accessor declaration opt>
         |  <remove accessor declaration> <raise accessor declaration opt>

  

<add accessor declaration>
        ::= <opt attributes> <opt modifiers> add  <block or semicolon>

<remove accessor declaration>
        ::= <opt attributes> <opt modifiers> remove <block or semicolon>

<raise accessor declaration opt> ::= <raise accessor declaration>
                                  |
                                  

<raise accessor declaration>
        ::= <opt attributes> <opt modifiers> raise  <block or semicolon>
         
<enum declaration>
        ::=<Opt Documentation>  <opt attributes> <opt modifiers> enum <Identifier> <opt enum base> '{' <opt enum member declarations> '}' <opt semicolon> 

<opt enum base>
        ::= 
         |  ':' <type> 


<opt enum member declarations>
        ::= 
         |  <enum member declarations> 
         |  <enum member declarations> ',' 

<enum member declarations>
        ::= <enum member declaration> 
         |  <enum member declarations> ',' <enum member declaration> 

<enum member declaration>
        ::= <Opt Documentation> <opt attributes> <Identifier>  
         |  <Opt Documentation> <opt attributes> <Identifier>  '='  <expression>  
    

<delegate declaration>
        ::=<Opt Documentation>  <opt attributes> <opt modifiers> delegate <member type> <type declaration name> '(' <opt formal parameter list> ')' <opt type parameter constraints clauses> ';' 



<type declaration name>
        ::= <Identifier>  <opt type parameter list> 

<method declaration name>
        ::= <type declaration name> 
         |  <explicit interface> <Identifier>  <opt type parameter list>
         
<indexer declaration name>
        ::= self 
         |  <explicit interface> self 

<explicit interface>
        ::= <explicit interface> <Identifier>  <opt type argument list> '.' 
         |  <Identifier>  <opt type argument list> '.' 
         



<class declaration>
        ::=<Opt Documentation>  <opt attributes> <opt modifiers> <opt partial> class <type declaration name> <opt class base> <opt type parameter constraints clauses> '{' <opt class member declarations> '}' <opt semicolon> 

       
